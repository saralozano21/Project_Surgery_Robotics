#include <WiFi.h>
#include <WiFiUdp.h>
#include "MPU9250.h"
#include <Wire.h> // Needed for I2C to read IMU
#include <ArduinoJson.h> // Compatible amb versiÃ³ 7.4.2
#include <IMU_RoboticsUB.h>   

// Device ID
const char *deviceId = "G2_Gri";

// Wi-Fi credentials
const char *ssid = "Robotics_UB";
const char *password = "rUBot_xx";

// Vibration motor settings
const int vibrationPin = 23; // Pin for the vibration motor

// Botons
const int PIN_S1 = 14;
const int PIN_S2 = 27;
int s1Status = HIGH;
int s2Status = HIGH;

// UDP settings
IPAddress receiverESP32IP(192, 168, 1, 23); // IP of receiver ESP32 (Servomotors)
IPAddress receiverComputerIP(192, 168, 1, 25); // IP of PC
const int udpPort = 12345;
WiFiUDP udp;

// IMU object
IMU imu;

// Orientation data
float Gri_roll = 0.0, Gri_pitch = 0.0, Gri_yaw = 0.0;

// Torques globals (rebuts del mÃ²dul Servomotors)
float torque_roll1 = 0.0;
float torque_roll2 = 0.0;
float torque_pitch = 0.0;
float torque_yaw   = 0.0;

void connectToWiFi() {
  Serial.print("Connecting to Wi-Fi");
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWi-Fi connected!");
  Serial.println("IP Address: " + WiFi.localIP().toString());
  Serial.print("ESP32 MAC Address: ");
  Serial.println(WiFi.macAddress());
}

void updateOrientation() {
  // Llegeix FIFO del DMP i actualitza cÃ lculs interns
  imu.ReadSensor();
  // ObtÃ© els angles (roll, pitch, yaw)
  float* rpw = imu.GetRPW();
  Gri_roll  = rpw[0];
  Gri_pitch = rpw[1];
  Gri_yaw   = rpw[2];
  s1Status = digitalRead(PIN_S1);
  s2Status = digitalRead(PIN_S2);
}

void sendOrientationUDP() {
  JsonDocument doc;
  doc["device"] = deviceId;
  doc["roll"] = Gri_roll;
  doc["pitch"] = Gri_pitch;
  doc["yaw"] = Gri_yaw;
  doc["s1"] = s1Status;
  doc["s2"] = s2Status;

  char jsonBuffer[512];
  serializeJson(doc, jsonBuffer, sizeof(jsonBuffer));

  // Send to ESP32 Servos
  udp.beginPacket(receiverESP32IP, udpPort);
  udp.write((const uint8_t*)jsonBuffer, strlen(jsonBuffer));
  udp.endPacket();

  // Send to Computer
  udp.beginPacket(receiverComputerIP, udpPort);
  udp.write((const uint8_t*)jsonBuffer, strlen(jsonBuffer));
  udp.endPacket();
}

// FunciÃ³ per rebre torques i controlar la vibraciÃ³
void receiveTorqueUDP() {
  int packetSize = udp.parsePacket();
  if (packetSize) {
    char incomingPacket[512];
    int len = udp.read(incomingPacket, sizeof(incomingPacket) - 1);
    if (len > 0) {
      incomingPacket[len] = '\0';
    }

    JsonDocument doc;
    DeserializationError error = deserializeJson(doc, incomingPacket);
    if (error) {
      Serial.print("deserializeJson() failed: ");
      Serial.println(error.f_str());
      return;
    }

    // Comprova si el missatge contÃ© els camps de torque
    if (doc.containsKey("torque_roll1")) {
      torque_roll1 = doc["torque_roll1"];
      torque_roll2 = doc["torque_roll2"];
      torque_pitch = doc["torque_pitch"];
      torque_yaw   = doc["torque_yaw"];

      Serial.println("=== Torques rebuts ===");
      Serial.print("Torque Roll1: "); Serial.println(torque_roll1);
      Serial.print("Torque Roll2: "); Serial.println(torque_roll2);
      Serial.print("Torque Pitch: "); Serial.println(torque_pitch);
      Serial.print("Torque Yaw: ");   Serial.println(torque_yaw);

      // ðŸŸ  Vibration motor control based on torque values
      float totalTorque = torque_roll1 + torque_pitch + torque_yaw;
      // Convert torque to PWM value (0-255)
      int vibrationValue = constrain(totalTorque * 2.5, 0, 255); // Adjust the scaling factor as needed
      ledcWrite(0, vibrationValue); // Set the PWM value for the vibration motor
      Serial.print("Vibration motor value: ");
      Serial.println(vibrationValue);

      Serial.println("======================");
    }
  }
}

void setup() {
  Serial.begin(115200);
  Wire.begin();
  delay(2000);

  // Inicialitza IMU (amb DMP)
  imu.Install();

  connectToWiFi();
  udp.begin(udpPort);
  Serial.println("UDP initialized");

  pinMode(PIN_S1, INPUT);
  pinMode(PIN_S2, INPUT);

  // ðŸŸ¢ Configure PWM for the vibration motor (channel 0)
  ledcSetup(0, 5000, 8); // Channel 0, frequency 5kHz, resolution 8 bits
  ledcAttachPin(vibrationPin, 0); // Attach the vibration motor to channel 0
}

void loop() {
  updateOrientation();
  sendOrientationUDP();
  receiveTorqueUDP(); 
  delay(10);
}
